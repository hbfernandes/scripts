#!/usr/bin/env groovy

// Generates the version properties for the dependencies that don't have them and prints them out
// Takes a pom.xml file as argument and prints the output out

import groovy.xml.XmlUtil

File pom = new File(args[0])
def root = new XmlSlurper(false, false, false).parseText(pom.text)

Map properties = [:]

println '### DEPENDENCIES ###'
root.dependencies.dependency.each { dependency ->
  String artifactId = dependency.artifactId
  String version = dependency.version

  if(!version.startsWith('${') && version){
    properties << [(artifactId + '.version'): version]
    dependency.version = "\${${artifactId}.version}"
    print (XmlUtil.serialize(dependency) - '<?xml version="1.0" encoding="UTF-8"?>')
  }
}

println '\n### PROPERTIES ###'
properties.each { k, v ->
  println "<$k>$v</$k>"
}
